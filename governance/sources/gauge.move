/// © 2025 Metabyte Labs, Inc.  All Rights Reserved.
/// 
/// The gauge module serves as a critical component in the decentralized liquidity incentivization 
/// and governance system. It functions as an intermediary that connects liquidity pools with token 
/// distribution mechanisms in a vote-directed rewards framework.
///
/// Core Functions:
/// - Liquidity Pool Incentivization: Gauges are tied to specific liquidity pools and distribute rewards 
///   to liquidity providers who stake their positions. The reward distribution is based on voting weight.
/// - Position Staking Management: Tracks staked positions, calculates rewards, and enables withdrawal.
/// - Reward Distribution: Maintains reward rates, calculates earned rewards based on staked liquidity 
///   and time, and processes reward claims to position owners.
/// - Fee Collection: Collects and manages fees generated by the associated liquidity pool.
module governance::gauge {
    const COPYRIGHT_NOTICE: vector<u8> = b"© 2025 Metabyte Labs, Inc.  All Rights Reserved.";
    const PATENT_NOTICE: vector<u8> = b"Patent pending - U.S. Patent Application No. 63/861,982";

    use sui::object_table::{Self, ObjectTable};
    use sui::table::{Self, Table};
    use sui::balance::{Self, Balance};
    use std::type_name::{Self, TypeName};
    use voting_escrow::common;
    use governance::distribution_config::{DistributionConfig};
    use governance::voter_cap::{VoterCap};

    const EInvalidVoter: u64 = 922337365605842945;

    const ENotifyEpochTokenInvalidPool: u64 = 4672810119034640000;
    const ENotifyEpochTokenDistributionConfInvalid: u64 = 218800936909496100;
    const ENotifyEpochTokenGaugeNotAlive: u64 = 213816814886850880;
    const ENotifyEpochTokenAlreadyNotifiedThisEpoch: u64 = 776925370166021200;
    const ENotifyEpochTokenEpochAlreadyStarted: u64 = 4159845750300726000;
    const ENotifyEpochTokenPrevRewardsNotFinished: u64 = 3254849085073565700;
    const ENotifyEpochTokenAlreadyNotifiedToken: u64 = 4041460742273430500;

    const ENotifyRewardGaugeNotAlive: u64 = 927999301192928000;
    const ENotifyRewardDistributionConfInvalid: u64 = 311080855434061200;
    const ENotifyRewardInvalidPool: u64 = 5832633373805671000;
    const ENotifyRewardInvalidAmount: u64 = 9223373716188102674;
    const ENotifyRewardEpochFinished: u64 = 256780623436252400;

    const ENullRewardDistributionConfInvalid: u64 = 803388616803070000;
    const ENullRewardInvalidPool: u64 = 119295864650511570;
    const ENullRewardEpochFinished: u64 = 453228670707964700;

    const EDepositPositionDistributionConfInvalid: u64 = 9223373183611043839;
    const EDepositPositionGaugeNotAlive: u64 = 9223373157842747416;
    const EDepositPositionGaugeDoesNotMatchPool: u64 = 9223373162136666120;
    const EDepositPositionPositionDoesNotMatchPool: u64 = 9223373166431764490;
    const EDepositPositionPositionAlreadyStaked: u64 = 9223373175021174786;
    const EDepositPositionHasNoLiquidity: u64 = 9223373172345436343;

    const EEarnedByPositionGaugeDoesNotMatchPool: u64 = 9223372693985230856;
    const EEarnedByPositionNotDepositedPosition: u64 = 9223372698279936004;

    const EGetCurrentGrowthGlobalGaugeDoesNotMatchPool: u64 = 319134756481074050;

    const EUpdateRewardGaugeDoesNotMatchPool: u64 = 922337345419444224;
    const EGetPositionRewardGaugeDoesNotMatchPool: u64 = 73727195005154130;

    const EAllRewardsClaimedGaugeDoesNotMatchPool: u64 = 577203795438525600;

    const ENotifyRewardWithoutClaimGaugeNotAlive: u64 = 388157577581832000;
    const ENotifyRewardWithoutClaimDistributionConfInvalid: u64 = 921517595696832600;
    const ENotifyRewardAmountRewardRateZero: u64 = 9223373952411435028;
    const ENotifyRewardWithoutClaimInvalidPool: u64 = 6794499896215460000;
    const ENotifyRewardWithoutClaimInvalidAmount: u64 = 9223373819267317778;

    const ESyncFullsailDistributionPriceInvalidEpoch: u64 = 524842288068695600;
    const ESyncOsailDistributionPriceDistributionConfInvalid: u64 = 490749102979896500;
    const ESyncOsailDistributionPriceGaugeNotAlive: u64 = 298752582283296830;
    const ESyncOsailDistributionPriceInvalidPool: u64 = 485510326827034900;

    const EReceiveGaugeCapGaugeDoesNotMatch: u64 = 9223373119186534399;

    const EWithdrawPositionNotDepositedPosition: u64 = 9223373570158297092;
    const EWithdrawPositionPositionIsLocked: u64 = 922337373443534534;
    const EWithdrawPositionNotAllRewardsClaimed: u64 = 51536857596176540;
    const EWithdrawPositionInvalidPool: u64 = 496532944256373500;

    const EFullEarnedForTypeGaugeDoesNotMatchPool: u64 = 266845958013316900;

    const ENotOwner: u64 = 9843325239567326443;

    const EInvalidLiquidity: u64 = 9934967927031223221;

    const UNCLAIMED_O_SAIL_KEY: vector<u8> = b"unclaimed_o_sail";

    /// Witness type for gauge module initialization.
    /// Used to ensure proper module initialization and access control.
    public struct GAUGE has drop {}

    public struct AdminCap has store, key {
        id: UID,
    }

    public struct StakedPosition has store, key {
        id: sui::object::UID,
        position_id: sui::object::ID,
        pool_id: sui::object::ID,
        description: std::string::String,
        name: std::string::String,
    }

    public struct EventNotifyEpochToken has copy, drop, store {
        sender: ID,
        gauge_id: ID,
        pool_id: ID,
        token: TypeName,
        // prev token is none if it is the first notify epoch call
        prev_token: Option<TypeName>,
        // growth global of the prev token, 0 if prev token is none
        growth_global_prev_token: u128,
    }

    public struct EventNotifyReward has copy, drop, store {
        sender: ID,
        usd_amount: u64,
    }

    public struct EventSyncOSailDistributionPrice has copy, drop, store {
        gauge_id: ID,
        pool_id: ID,
        price_q64: u128,
        remaining_o_sail_q64: u128,
        remaining_usd_q64: u128,
    }

    public struct EventClaimFees has copy, drop, store {
        amount_a: u64,
        amount_b: u64,
    }

    public struct RewardProfile has store {
        growth_inside: u128,
        amount: u64,
        last_update_time: u64,
    }

    public struct EventWithdrawPosition has copy, drop, store {
        staked_position_id: ID,
        position_id: ID,
        gauger_id: ID,
        pool_id: ID,
    }

    public struct EventDepositGauge has copy, drop, store {
        gauger_id: ID,
        pool_id: ID,
        position_id: ID,
        staked_position_id: ID,
        start_growth_inside: u128
    }

    public struct EventGaugeSetVoter has copy, drop, store {
        id: ID,
        voter_id: ID,
    }

    public struct EventUpdateRewardPosition has copy, drop, store {
        gauger_id: ID,
        pool_id: ID,
        position_id: ID,
        growth_inside: u128,
        amount: u64,
    }

    public struct EventNullRewards has copy, drop, store {
        gauge_id: ID,
        pool_id: ID,
        rewards_nulled_usd: u64,
    }

    public struct EventAddUnclaimedOsail has copy, drop, store {
        gauge_id: ID,
        amount: u64,
    }

    public struct EventRemoveUnclaimedOsail has copy, drop, store {
        gauge_id: ID,
        amount: u64,
    }

    public struct EventCollectEarlyWithdrawalPenalty has copy, drop, store {
        gauge_id: ID,
        position_id: ID,
        early_withdrawal_penalty: u128,
        penalty_amount_a: u64,
        penalty_amount_b: u64,
        fee_a: u64,
        fee_b: u64,
        timestamp: u64,
    }
    public struct Locked has copy, drop, store {}

    public struct Gauge<phantom CoinTypeA, phantom CoinTypeB> has store, key {
        id: UID,
        pool_id: ID,
        gauge_cap: Option<gauge_cap::gauge_cap::GaugeCap>,
        distribution_config: ID,
        staked_positions: ObjectTable<ID, clmm_pool::position::Position>,
        locked_positions: Table<ID, Locked>,
        // distribution_growth is also calculated by all tokens
        // current epoch token may not be set if gauge has not participated in any distributions
        current_epoch_token: Option<TypeName>,
        // timestamp of the last call of the notify_epoch_token function
        epoch_token_last_notified: u64,
        fee_a: Balance<CoinTypeA>,
        fee_b: Balance<CoinTypeB>,
        voter: Option<ID>,
        usd_reward_rate: u128,
        o_sail_reward_rate: u128,
        period_finish: u64,
        usd_reward_rate_by_epoch: Table<u64, u128>,
        // Distributed o_sail amount by epoch
        o_sail_emission_by_epoch: Table<u64, u64>,
        // Checkpoint of distribution reserve to calculate distributed amount.
        // Delta between checkpoints represents distributed amount
        last_distribution_reserve: u64,
        rewards: Table<ID, RewardProfile>,
        // bag to be preapred for future updates
        bag: sui::bag::Bag,
    }

    public fun notices(): (vector<u8>, vector<u8>) {
        (COPYRIGHT_NOTICE, PATENT_NOTICE)
    }

    /// Initializes the gauge module by setting up display fields and transferring ownership.
    /// This function is called during module initialization to:
    /// * Set up display fields for staked position objects
    /// * Configure metadata display for the Fullsail Finance interface
    /// * Transfer display and publisher objects to the module owner
    /// 
    /// # Arguments
    /// * `gauge_witness` - Witness type for gauge module initialization
    /// * `ctx` - Mutable reference to the transaction context
    /// 
    /// # Details
    /// Sets up the following display fields:
    /// * name - Staked position name
    /// * link - Staked position link in Fullsail Finance app
    /// * image_url - Staked position image URL
    /// * description - Staked position description
    /// * website - Project website
    /// * creator - Staked position creator
    /// 
    /// # Transfers
    /// * Transfers the Display<StakedPosition> object to the transaction sender
    /// * Transfers the Publisher object to the transaction sender
    fun init(gauge_witness: GAUGE, ctx: &mut sui::tx_context::TxContext) {

        let publisher = sui::package::claim<GAUGE>(gauge_witness, ctx);

        let display = update_display(
            &publisher,
            std::string::utf8(b"{name}"),
            std::string::utf8(b"https://app.fullsail.finance/liquidity/{pool_id}/positions/{position_id}"),
            std::string::utf8(b"https://app.fullsail.finance/static_files/staked_position.png"),
            std::string::utf8(b"{description}"),
            std::string::utf8(b"https://app.fullsail.finance"),
            std::string::utf8(b"FULLSAIL"),
            ctx
        );

        sui::transfer::public_transfer<sui::display::Display<StakedPosition>>(display, sui::tx_context::sender(ctx));
        sui::transfer::public_transfer<sui::package::Publisher>(publisher, sui::tx_context::sender(ctx));
    }

    #[test_only]
    /// Test initialization of the gauge system
    /// Replicates the init function logic for testing purposes
    public fun test_init(ctx: &mut sui::tx_context::TxContext) {
        
        let publisher = sui::package::claim<GAUGE>(GAUGE{}, ctx);

        let display = update_display(
            &publisher,
            std::string::utf8(b"{name}"),
            std::string::utf8(b"https://app.fullsail.finance/liquidity/{pool_id}/positions/{position_id}"),
            std::string::utf8(b"https://app.fullsail.finance/static_files/staked_position.png"),
            std::string::utf8(b"{description}"),
            std::string::utf8(b"https://app.fullsail.finance"),
            std::string::utf8(b"FULLSAIL"),
            ctx
        );

        sui::transfer::public_transfer<sui::display::Display<StakedPosition>>(display, sui::tx_context::sender(ctx));
        sui::transfer::public_transfer<sui::package::Publisher>(publisher, sui::tx_context::sender(ctx));
    }

    /// Returns the pool ID associated with the gauge.
    public fun pool_id<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>
    ): ID {
        gauge.pool_id
    }

    /// Checks if the gauge and pool are correctly associated with each other.
    public fun check_gauger_pool<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>
    ): bool {
        gauge.pool_id == object::id(pool)
    }

    /// Verifies that the voter capability matches the voter ID stored in the gauge.
    fun validate_voter_cap<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        voter_cap: &VoterCap
    ) {
        let voter_id = voter_cap.get_voter_id();
        assert!(&voter_id == gauge.voter.borrow(), EInvalidVoter);
    }

    /// Claims accumulated fees from the associated pool.
    public fun claim_fees<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        _distribute_cap: &governance::distribute_cap::DistributeCap,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>
    ): (Balance<CoinTypeA>, Balance<CoinTypeB>) {
        gauge.claim_fees_internal(pool)
    }

    /// Internal function to claim fees from the pool and handle fee accounting.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The pool to claim fees from
    ///
    /// # Returns
    /// A tuple of balances for CoinTypeA and CoinTypeB
    fun claim_fees_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>
    ): (Balance<CoinTypeA>, Balance<CoinTypeB>) {
        let epochCoinPerSecond = common::epoch();
        let (fee_a, fee_b) = pool.collect_fullsail_distribution_gauger_fees(gauge.gauge_cap.borrow());
        
        // Check if there are fees from pool or existing fees in gauge
        let existing_amount_a = gauge.fee_a.value<CoinTypeA>();
        let existing_amount_b = gauge.fee_b.value<CoinTypeB>();
        let pool_fee_a = fee_a.value<CoinTypeA>();
        let pool_fee_b = fee_b.value<CoinTypeB>();
        
        if (pool_fee_a > 0 || pool_fee_b > 0 || existing_amount_a > 0 || existing_amount_b > 0) {
            let amount_a = gauge.fee_a.join<CoinTypeA>(fee_a);
            let amount_b = gauge.fee_b.join<CoinTypeB>(fee_b);
            let withdrawn_a = if (epochCoinPerSecond > 0 && (amount_a / epochCoinPerSecond) > 0) { // amount per second is greater than 0
                gauge.fee_a.withdraw_all<CoinTypeA>()
            } else {
                balance::zero<CoinTypeA>()
            };
            let withdraw_b = if (epochCoinPerSecond > 0 && (amount_b / epochCoinPerSecond) > 0) {
                gauge.fee_b.withdraw_all<CoinTypeB>()
            } else {
                balance::zero<CoinTypeB>()
            };
            let claim_fees_event = EventClaimFees {
                amount_a,
                amount_b,
            };
            sui::event::emit<EventClaimFees>(claim_fees_event);
            return (withdrawn_a, withdraw_b)
        };
        fee_a.destroy_zero();
        fee_b.destroy_zero();
        (balance::zero<CoinTypeA>(), balance::zero<CoinTypeB>())
    }

    /// Creates a new gauge for the specified pool.
    ///
    /// # Arguments
    /// * `distribution_config` - Global distribution configuration
    /// * `pool_id` - ID of the pool this gauge will be associated with
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// A new gauge instance
    public(package) fun create<CoinTypeA, CoinTypeB>(
        distribution_config: &DistributionConfig,
        pool_id: ID,
        ctx: &mut TxContext
    ): Gauge<CoinTypeA, CoinTypeB> {
        let id = object::new(ctx);
        Gauge<CoinTypeA, CoinTypeB> {
            id,
            pool_id,
            gauge_cap: option::none<gauge_cap::gauge_cap::GaugeCap>(),
            distribution_config: object::id(distribution_config),
            staked_positions: object_table::new<ID, clmm_pool::position::Position>(ctx),
            locked_positions: table::new<ID, Locked>(ctx),
            current_epoch_token: option::none(),
            epoch_token_last_notified: 0,
            fee_a: balance::zero<CoinTypeA>(),
            fee_b: balance::zero<CoinTypeB>(),
            voter: option::none<ID>(),
            usd_reward_rate: 0,
            o_sail_reward_rate: 0,
            period_finish: 0,
            usd_reward_rate_by_epoch: table::new<u64, u128>(ctx),
            o_sail_emission_by_epoch: table::new<u64, u64>(ctx),
            last_distribution_reserve: 0,
            rewards: table::new<ID, RewardProfile>(ctx),
            bag: sui::bag::new(ctx),
        }
    }

    /// Returns the position ID for a staked position
    /// 
    /// # Arguments
    /// * `staked_position` - The staked position to get ID for
    /// 
    /// # Returns
    public fun position_id(
        staked_position: &StakedPosition,
    ): sui::object::ID {
        staked_position.position_id
    }

    /// Deposits a position into the gauge for staking and reward accrual.
    /// This function is called by liquidity providers to stake their positions
    /// and start earning rewards based on voting weight.
    ///
    /// # Arguments
    /// * `global_config` - Global configuration for the CLMM pool
    /// * `distribution_config` - Distribution configuration
    /// * `gauge` - The gauge instance
    /// * `pool` - The pool where the position is from
    /// * `position` - The position to deposit
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Aborts
    /// * If distribution config is invalid for the gauge
    /// * If the gauge is not alive
    /// * If the gauge does not match the pool
    /// * If the position does not match the pool
    /// * If the position is already staked
    public fun deposit_position<CoinTypeA, CoinTypeB>(
        global_config: &clmm_pool::config::GlobalConfig,
        distribution_config: &DistributionConfig,
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position: clmm_pool::position::Position,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): StakedPosition {
        distribution_config.checked_package_version();
        let sender = tx_context::sender(ctx);
        let pool_id = object::id(pool);
        let position_id = object::id<clmm_pool::position::Position>(&position);
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            EDepositPositionDistributionConfInvalid
        );
        assert!(
            distribution_config.is_gauge_alive(object::id(gauge)),
            EDepositPositionGaugeNotAlive
        );
        assert!(
            gauge.check_gauger_pool(pool),
            EDepositPositionGaugeDoesNotMatchPool
        );
        assert!(position.pool_id() == pool_id, EDepositPositionPositionDoesNotMatchPool);
        assert!(position.liquidity() > 0, EDepositPositionHasNoLiquidity);
        assert!(
            !pool.position_manager().borrow_position_info(position_id).is_staked(),
            EDepositPositionPositionAlreadyStaked
        );
        let (fee_a, fee_b) = clmm_pool::pool::collect_fee<CoinTypeA, CoinTypeB>(
            global_config,
            pool,
            &position,
            true
        );
        transfer::public_transfer<sui::coin::Coin<CoinTypeA>>(
            sui::coin::from_balance<CoinTypeA>(fee_a, ctx),
            sender
        );
        transfer::public_transfer<sui::coin::Coin<CoinTypeB>>(
            sui::coin::from_balance<CoinTypeB>(fee_b, ctx),
            sender
        );
        
        deposit_position_internal(
            gauge, 
            pool, 
            position, 
            clock, 
            ctx,
        )
    }

    /// Deposits a position into the gauge through the locker.
    /// This function allows the locker to deposit a position into the gauge for staking and reward distribution.
    /// 
    /// # Arguments
    /// * `gauge` - Mutable reference to the gauge contract managing rewards
    /// * `_locker_cap` - Capability proving locker authorization
    /// * `pool` - Mutable reference to the CLMM pool where the position exists
    /// * `position` - The position to be deposited
    /// * `clock` - System clock for time-based operations
    /// * `ctx` - Transaction context
    /// 
    /// # Aborts
    /// * If the position has no liquidity
    /// * If the gauge does not match the pool
    /// * If the position does not belong to the specified pool
    /// * If the position is already staked
    public fun deposit_position_by_locker<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        _locker_cap: &locker_cap::locker_cap::LockerCap,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position: clmm_pool::position::Position,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): StakedPosition {
        // this method is called by the locker package, so version control is solved by the locker package.
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            EDepositPositionDistributionConfInvalid
        );
        assert!(
            distribution_config.is_gauge_alive(object::id(gauge)),
            EDepositPositionGaugeNotAlive
        );
        let pool_id = object::id(pool);
        assert!(position.liquidity() > 0, EDepositPositionHasNoLiquidity);
        let position_id = object::id<clmm_pool::position::Position>(&position);
        assert!(
            gauge.check_gauger_pool(pool),
            EDepositPositionGaugeDoesNotMatchPool
        );
        assert!(position.pool_id() == pool_id, EDepositPositionPositionDoesNotMatchPool);
        assert!(
            !pool.position_manager().borrow_position_info(position_id).is_staked(),
            EDepositPositionPositionAlreadyStaked
        );
        deposit_position_internal(
            gauge, 
            pool, 
            position, 
            clock, 
            ctx,
        )
    }

    /// Internal function to deposit a position into the gauge.
    /// This function handles the core logic of depositing a position, including:
    /// - Creating and storing position stake information
    /// - Managing position tracking in the gauge
    /// - Setting up reward profiles
    /// - Staking the position in the pool's distribution system
    /// 
    /// # Arguments
    /// * `gauge` - Mutable reference to the gauge contract managing rewards
    /// * `pool` - Mutable reference to the CLMM pool where the position exists
    /// * `position` - The position to be deposited
    /// * `clock` - System clock for time-based operations
    /// * `ctx` - Transaction context
    fun deposit_position_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position: clmm_pool::position::Position,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): StakedPosition {
        let position_id = object::id<clmm_pool::position::Position>(&position);
        let pool_id = object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool);
        assert!(pool_id == position.pool_id(), EDepositPositionGaugeDoesNotMatchPool);
        
        let (lower_tick, upper_tick) = position.tick_range();
        let gauge_cap = gauge.gauge_cap.borrow();
        pool.update_fullsail_distribution_growth_global(gauge_cap, clock);
        if (!gauge.rewards.contains(position_id)) {
            let new_reward_profile = RewardProfile {
                growth_inside: pool.get_fullsail_distribution_growth_inside(lower_tick, upper_tick, 0),
                amount: 0,
                last_update_time: clock.timestamp_ms() / 1000,
            };
            gauge.rewards.add(position_id, new_reward_profile);
        } else {
            let reward_profile = gauge.rewards.borrow_mut(position_id);
            reward_profile.growth_inside = pool.get_fullsail_distribution_growth_inside(lower_tick, upper_tick, 0);
            reward_profile.last_update_time = clock.timestamp_ms() / 1000;
        };

        pool.stake_in_fullsail_distribution(
            gauge_cap,
            &position,
            clock
        );

        let staked_position = StakedPosition{
            id: sui::object::new(ctx),
            position_id: position_id,
            pool_id: position.pool_id(),
            description: std::string::utf8(b"Full Sail Staked Liquidity Position"),
            name: new_staked_position_name(pool.index(), position.index()),
        };

        gauge.staked_positions.add(position_id, position);

        let deposit_gauge_event = EventDepositGauge {
            gauger_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            pool_id: object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool),
            position_id,
            staked_position_id: object::id<StakedPosition>(&staked_position),
            start_growth_inside: gauge.rewards.borrow(position_id).growth_inside
        };
        sui::event::emit<EventDepositGauge>(deposit_gauge_event);

        staked_position
    }

    fun new_staked_position_name(pool_index: u64, position_index: u64): std::string::String {
        let mut staked_position_name = std::string::utf8(b"Fullsail Staked Position:");
        std::string::append(&mut staked_position_name, clmm_pool::utils::str(pool_index));
        std::string::append_utf8(&mut staked_position_name, b"-");
        std::string::append(&mut staked_position_name, clmm_pool::utils::str(position_index));
        staked_position_name
    }

    /// Function to calculate earned rewards for a position.
    /// Does not consider current epoch oSAIL token.
    /// This is a complex calculation that takes into account time elapsed,
    /// global reward growth, and position-specific factors.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `position_id` - ID of the position
    /// * `time` - Current timestamp in seconds
    ///
    /// # Returns
    /// * The amount of rewards earned by the position
    public fun earned<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        clock: &sui::clock::Clock
    ): u64 {
        assert!(
            gauge.check_gauger_pool(pool),
            EEarnedByPositionGaugeDoesNotMatchPool
        );
        if (!gauge.staked_positions.contains(position_id)) {
            return 0
        };
        let (amount, _) = gauge.earned_internal<CoinTypeA, CoinTypeB>(pool, position_id, clock.timestamp_ms() / 1000);
        amount
    }

    /// Internal function to calculate earned rewards for a position.
    /// This is a complex calculation that takes into account time elapsed,
    /// global reward growth, and position-specific factors.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `position_id` - ID of the position
    /// * `time` - Current timestamp in seconds
    ///
    /// # Returns
    /// * The amount of rewards earned by the position
    /// * Growth inside of the position
    fun earned_internal<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        time: u64
    ): (u64, u128) {
        
        let current_growth_global = get_current_growth_global(gauge, pool, time);

        let position = gauge.staked_positions.borrow(position_id);
        let (lower_tick, upper_tick) = position.tick_range();
        let new_growth_inside = pool.get_fullsail_distribution_growth_inside(
            lower_tick,
            upper_tick,
            current_growth_global
        );

        let mut claimed_growth_inside = gauge.rewards.borrow(position_id).growth_inside;

        let growth_inside_diff = integer_mate::math_u128::wrapping_sub(new_growth_inside, claimed_growth_inside);
        // assert!(!integer_mate::math_u128::is_neg(growth_inside_diff), EIncorrectGrowthInside);
        if (integer_mate::math_u128::is_neg(growth_inside_diff)) {
            return (0, new_growth_inside)
        };

        let amount_earned = integer_mate::full_math_u128::mul_div_floor(
            growth_inside_diff,
            position.liquidity(),
            1 << 64
        ) as u64;

        (amount_earned, new_growth_inside)
    }

    #[test_only]
    public fun test_earned_internal<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        time: u64
    ): (u64, u128) {
        gauge.earned_internal<CoinTypeA, CoinTypeB>(pool, position_id, time)
    }

    /// Calculates the current global growth value for reward distribution.
    /// This function determines how much oSAIL rewards have accumulated globally since the last update,
    /// taking into account the reward rate, time elapsed, and available distribution reserves.
    /// 
    /// # Arguments
    /// * `gauge` - Reference to the gauge contract managing rewards
    /// * `pool` - Reference to the CLMM pool where rewards are being distributed
    /// * `current_time` - Current timestamp in seconds
    /// 
    /// # Returns
    /// The updated global growth value in Q64.64 fixed-point format
    /// 
    /// # Details
    /// The function:
    /// 1. Gets the current global growth from the pool
    /// 2. Calculates time elapsed since last update
    /// 3. Checks if growth should be updated based on:
    ///    - Positive time elapsed
    ///    - Available distribution reserves
    ///    - Non-zero staked liquidity
    /// 4. If update is needed:
    ///    - Calculates potential reward amount based on reward rate and time
    ///    - Caps reward amount to available reserves
    ///    - Updates global growth by distributing rewards proportionally to staked liquidity
    public fun get_current_growth_global<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        current_time: u64,
    ): u128 {
        assert!(
            gauge.check_gauger_pool(pool),
            EGetCurrentGrowthGlobalGaugeDoesNotMatchPool
        );
        let mut growth_global = pool.get_fullsail_distribution_growth_global();
        let time_since_last_update = current_time - pool.get_fullsail_distribution_last_updated();

        let staked_liquidity = pool.get_fullsail_distribution_staked_liquidity();
        let distribution_reseve_q64 = (pool.get_fullsail_distribution_reserve() as u128) * (1 << 64);
        let should_update_growth = time_since_last_update >= 0 && 
          distribution_reseve_q64 > 0 &&
          staked_liquidity > 0;

        if (should_update_growth) {
            let mut potential_reward_amount = gauge.o_sail_reward_rate * (time_since_last_update as u128);
            if (potential_reward_amount > distribution_reseve_q64) {
                potential_reward_amount = distribution_reseve_q64;
            };
            growth_global = growth_global + integer_mate::math_u128::checked_div_round(
                potential_reward_amount,
                staked_liquidity,
                false
            );
        };

        growth_global
    }

    /// Calculates the current growth inside a specific position's tick range.
    /// This function determines how much reward growth has accumulated within the position's boundaries.
    /// 
    /// # Arguments
    /// * `gauge` - Reference to the gauge managing the position
    /// * `pool` - Reference to the CLMM pool associated with the position
    /// * `position_id` - ID of the position to calculate growth for
    /// * `time` - Current timestamp for growth calculation
    /// 
    /// # Returns
    /// The accumulated growth inside the position's tick range in Q64.64 fixed-point format
    /// 
    /// # Aborts
    /// * If the gauge does not match the pool
    /// * If the position is not staked in the gauge
    public fun get_current_growth_inside<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        time: u64,
    ): u128 {
        assert!(
            gauge.check_gauger_pool(pool),
            EEarnedByPositionGaugeDoesNotMatchPool
        );
        assert!(
            gauge.staked_positions.contains(position_id),
            EEarnedByPositionNotDepositedPosition
        );

        let current_growth_global = get_current_growth_global(gauge, pool, time);

        let position = gauge.staked_positions.borrow(position_id);
        let (lower_tick, upper_tick) = position.tick_range();
        pool.get_fullsail_distribution_growth_inside(
            lower_tick,
            upper_tick,
            current_growth_global
        )
    }

    /// Deprecated
    /// Broken method that exists only to keep other package building and prevent backend from crashing.
    public fun full_earned_for_type<CoinTypeA, CoinTypeB, RewardCoinType>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        last_growth_inside: u128,
        clock: &sui::clock::Clock,
    ): (u64, u128) {
        assert!(
            gauge.check_gauger_pool(pool),
            EFullEarnedForTypeGaugeDoesNotMatchPool
        );
        assert!(
            gauge.staked_positions.contains(position_id),
            EEarnedByPositionNotDepositedPosition
        );
        let coin_type = type_name::get<RewardCoinType>();
        if (gauge.is_valid_epoch_token<CoinTypeA, CoinTypeB, RewardCoinType>()) {
            let growth_global = gauge.get_current_growth_global(pool, clock.timestamp_ms() / 1000);
            let position = gauge.staked_positions.borrow(position_id);
            let (lower_tick, upper_tick) = position.tick_range();
            let growth_inside = pool.get_fullsail_distribution_growth_inside(
                lower_tick,
                upper_tick,
                growth_global
            );
            let growth_inside_diff = integer_mate::math_u128::wrapping_sub(growth_inside, last_growth_inside);
            if (integer_mate::math_u128::is_neg(growth_inside_diff)) {
                return (0, growth_inside)
            };
            let amount_earned = integer_mate::full_math_u128::mul_div_floor(
                growth_inside_diff,
                position.liquidity(),
                1 << 64
            ) as u64;
            return (amount_earned, growth_inside)
        };

        (0, 0)
    }
    /// Collects rewards for a specific pool position.
    /// 
    /// # Arguments
    /// * `global_config` - The global configuration
    /// * `rewarder_vault` - The rewarder vault
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `staked_position` - The staked position
    /// * `clock` - The system clock
    ///
    /// # Returns
    /// Balance of the collected rewards
    public fun get_pool_reward<CoinTypeA, CoinTypeB, RewardCoinType>(
        global_config: &clmm_pool::config::GlobalConfig,
        rewarder_vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: &StakedPosition,
        clock: &sui::clock::Clock
    ): sui::balance::Balance<RewardCoinType> {
        distribution_config.checked_package_version();
        assert!(
            gauge.check_gauger_pool(pool),
            EGetPositionRewardGaugeDoesNotMatchPool
        );

        let position = gauge.staked_positions.borrow(staked_position.position_id);
        let reward = clmm_pool::pool::collect_reward<CoinTypeA, CoinTypeB, RewardCoinType>(
            global_config,
            pool,
            position,
            rewarder_vault,
            true,
            clock
        );

        let current_time = clock.timestamp_ms() / 1000;
        let reward_profile = gauge.rewards.borrow(staked_position.position_id);
        if (reward_profile.last_update_time > (current_time - distribution_config.get_liquidity_update_cooldown())) {
            // return the reward to the rewarder vault
            clmm_pool::rewarder::deposit_reward(global_config, rewarder_vault, reward);

            return sui::balance::zero<RewardCoinType>()
        };

        reward
    }

    public fun get_pool_reward_v2<CoinTypeA, CoinTypeB, RewardCoinType>(
        global_config: &clmm_pool::config::GlobalConfig,
        rewarder_vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: &StakedPosition,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ): sui::balance::Balance<RewardCoinType> {
        distribution_config.checked_package_version();
        assert!(
            gauge.check_gauger_pool(pool),
            EGetPositionRewardGaugeDoesNotMatchPool
        );

        let position = gauge.staked_positions.borrow(staked_position.position_id);
        let reward = clmm_pool::pool::collect_reward<CoinTypeA, CoinTypeB, RewardCoinType>(
            global_config,
            pool,
            position,
            rewarder_vault,
            true,
            clock
        );

        if (gauge.is_withdrawal_restricted(distribution_config, staked_position.position_id(), clock, ctx)) {
            // return the reward to the rewarder vault
            clmm_pool::rewarder::deposit_reward(global_config, rewarder_vault, reward);

            return sui::balance::zero<RewardCoinType>()
        };

        reward
    }

    /// Checks if withdrawal restrictions are active for a staked_position.
    /// Returns true if restrictions apply (e.g., cooldown period or other limitations).
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `distribution_config` - Distribution configuration
    /// * `position_id` - The position ID to check
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    public fun is_withdrawal_restricted<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        position_id: ID,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ): bool {
        let current_time = clock.timestamp_ms() / 1000;
        let reward_profile = gauge.rewards.borrow(position_id);
        (
            !distribution_config.contains_unrestricted_address(ctx.sender()) &&
            reward_profile.last_update_time > (current_time - distribution_config.get_liquidity_update_cooldown())
        )
    }

    /// Sets current_epoch_token. Only current_epoch_token can be distributed in current epoch via Gauge.
    /// After this function is called all notify_reward calls will check that coin is allowed to be distributed.
    /// Returns undistributed reserves of previous epoch token.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `voter_cap` - Capability to notify rewards
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// The amount actual oSAIL emissions for the ended epoch.
    public fun notify_epoch_token<CoinTypeA, CoinTypeB, NextRewardCoinType>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        voter_cap: &VoterCap,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext,
    ): u64 {
        // this method is called by the voter package, so version control is solved by the voter package.
        gauge.validate_voter_cap(voter_cap);
        assert!(gauge.check_gauger_pool(pool), ENotifyEpochTokenInvalidPool);
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            ENotifyEpochTokenDistributionConfInvalid
        );
        assert!(
            distribution_config.is_gauge_alive(object::id(gauge)),
            ENotifyEpochTokenGaugeNotAlive
        );
        // cannot update to the same token
        assert!(
            gauge.current_epoch_token.is_none() || 
            !gauge.is_valid_epoch_token<CoinTypeA, CoinTypeB, NextRewardCoinType>(),
            ENotifyEpochTokenAlreadyNotifiedToken
        );

        // you can only change token in new epoch, before any rewards notified.
        // That's because growth_global cannot be mixed inside one epoch.
        let current_time = clock.timestamp_ms() / 1000;
        assert!(current_time >= gauge.period_finish, ENotifyEpochTokenEpochAlreadyStarted);
        let last_notified_period = common::to_period(gauge.epoch_token_last_notified);
        assert!(
            current_time >= last_notified_period + common::epoch(),
            ENotifyEpochTokenAlreadyNotifiedThisEpoch
        );
        let next_epoch_time = common::epoch_next(current_time);

        // update distribution. All rewards from previous epoch should be either distributed or burnt
        let gauge_cap = gauge.gauge_cap.borrow();
        pool.update_fullsail_distribution_growth_global(gauge_cap, clock);
        let fullsail_distribution_reserves = pool.get_fullsail_distribution_reserve();
        assert!(fullsail_distribution_reserves == 0, ENotifyEpochTokenPrevRewardsNotFinished);
        gauge.period_finish = next_epoch_time;
        // New token distribution starts from scratch, so we are nulling all the rates.
        // You may notice that pool reward rates are not 0 yet, but reserve is zero, so reward rates are not applied.
        // We will set the reward rates and reserves to the new values and will start applying them
        // when we call sync_fullsail_distribution_reward.
        gauge.usd_reward_rate = 0;
        gauge.o_sail_reward_rate = 0;

        // Since fullsail_distribution_reserves == 0 by assertion above, we do not subtract it from last_distribution_reserve.
        let distribution_reserve_delta = gauge.last_distribution_reserve;
        let current_emission = if (gauge.o_sail_emission_by_epoch.contains(last_notified_period)) {
            gauge.o_sail_emission_by_epoch.remove(last_notified_period)
        } else {
            0
        };
        // Here, last_distribution_reserve needs to be updated to fullsail_distribution_reserves (currently zero).
        gauge.last_distribution_reserve = 0;
        let ended_epoch_o_sail_emission = current_emission + distribution_reserve_delta;
        gauge.o_sail_emission_by_epoch.add(last_notified_period, ended_epoch_o_sail_emission);

        let coin_type = type_name::get<NextRewardCoinType>();
        let mut event = EventNotifyEpochToken {
            sender: object::id_from_address(tx_context::sender(ctx)),
            gauge_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            pool_id: object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool),
            token: coin_type,
            prev_token: option::none(),
            growth_global_prev_token: 0,
        };
        if (gauge.current_epoch_token.is_some()) {
            let prev_epoch_token = gauge.current_epoch_token.extract();

            // last growth_global that corresponds to the **previous** token.
            let growth_global = pool.get_fullsail_distribution_growth_global();
            event.prev_token.fill(prev_epoch_token);
            event.growth_global_prev_token = growth_global;
        };
        // Update TokenName state
        gauge.current_epoch_token.fill(coin_type);

        gauge.epoch_token_last_notified = current_time;

        sui::event::emit<EventNotifyEpochToken>(event);

        ended_epoch_o_sail_emission
    }

     /// Adds new rewards to the gauge without claiming accumulated fees.
    /// Similar to notify_reward but skips the fee claiming step.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `usd_amount` - The amount of USD to distribute, decimals 6
    /// * `o_sail_price_q64` - The oSAIL price in USD in Q64.64 format, in SAIL token decimals
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Aborts
    /// * If the voter capability is invalid
    /// * If the reward amount is invalid (zero)
    public(package) fun notify_reward_without_claim<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        usd_amount: u64,
        o_sail_price_q64: u128,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ) {
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            ENotifyRewardWithoutClaimDistributionConfInvalid
        );
        assert!(distribution_config.is_gauge_alive(object::id(gauge)), ENotifyRewardWithoutClaimGaugeNotAlive);
        assert!(gauge.check_gauger_pool(pool), ENotifyRewardWithoutClaimInvalidPool);

        assert!(usd_amount > 0, ENotifyRewardWithoutClaimInvalidAmount);
        let current_time = clock.timestamp_ms() / 1000;
        assert!(current_time < gauge.period_finish, ENotifyRewardEpochFinished);
        gauge.notify_reward_amount_internal<CoinTypeA, CoinTypeB>(usd_amount, clock);

        gauge.sync_o_sail_distribution_price_internal(pool, o_sail_price_q64, false, clock);
    }

    /// Adds new rewards to the gauge, claims accumulated fees, and updates reward rates.
    /// This function is called by authorized voters to direct rewards to this gauge.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `usd_amount` - The amount of USD to distribute, decimals 6
    /// * `o_sail_price_q64` - The oSAIL price in USD in Q64.64 format, in SAIL token decimals
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// A tuple of claimed fees in CoinTypeA and CoinTypeB
    ///
    /// # Aborts
    /// * If the voter capability is invalid
    /// * If the reward amount is invalid (zero)
    public(package) fun notify_reward<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        usd_amount: u64,
        o_sail_price_q64: u128,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): (Balance<CoinTypeA>, Balance<CoinTypeB>) {
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            ENotifyRewardDistributionConfInvalid
        );
        assert!(distribution_config.is_gauge_alive(object::id(gauge)), ENotifyRewardGaugeNotAlive);
        assert!(gauge.check_gauger_pool(pool), ENotifyRewardInvalidPool);
        assert!(usd_amount > 0, ENotifyRewardInvalidAmount);
        let current_time = clock.timestamp_ms() / 1000;
        assert!(current_time < gauge.period_finish, ENotifyRewardEpochFinished);

        let (fee_a, fee_b) = gauge.claim_fees_internal(pool);
        gauge.notify_reward_amount_internal<CoinTypeA, CoinTypeB>(usd_amount, clock);

        gauge.sync_o_sail_distribution_price_internal(pool, o_sail_price_q64, false, clock);
        (fee_a, fee_b)
    }

    /// Nulls the reward rate of the gauge. Updates all of the emissions counters and emits an event.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `o_sail_price_q64` - The oSAIL price in USD in Q64.64 format, in SAIL token decimals
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// a usd amount of oSAIL that is NOT going to be distributed because of this method call.
    /// This amount is needed to update counters in the Minter contract.
    ///
    /// # Aborts
    /// * If the voter capability is invalid
    /// * If the reward amount is invalid (zero)
    public(package) fun null_rewards<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        o_sail_price_q64: u128,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): u64 {
         assert!(
            object::id(distribution_config) == gauge.distribution_config,
            ENullRewardDistributionConfInvalid
        );
        assert!(gauge.check_gauger_pool(pool), ENullRewardInvalidPool);
        let current_time = clock.timestamp_ms() / 1000;
        assert!(current_time < gauge.period_finish, ENullRewardEpochFinished);

        // First: sync oSAIL price to update remaining amounts and distributed amounts.
        // no event is emitted to not confuse indexers with double events.
        gauge.sync_o_sail_distribution_price_internal(pool, o_sail_price_q64, true, clock);

        let (future_rewards, _) = calc_future_rewards(gauge, current_time);

        gauge.usd_reward_rate = 0;

        gauge.sync_o_sail_distribution_price_internal(pool, o_sail_price_q64, false, clock);

        let event = EventNullRewards {
            gauge_id: object::id(gauge),
            pool_id: object::id(pool),
            rewards_nulled_usd: future_rewards,
        };
        sui::event::emit<EventNullRewards>(event);

        future_rewards
    }

    // Calculates rewards remaining in the current epoch and time untile the end of the epoch (i.e. until this rewards are disributed)
    fun calc_future_rewards<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        current_time: u64,
    ): (u64, u64) {
        let next_epoch_time = common::epoch_next(current_time);
        let time_until_next_epoch = next_epoch_time - current_time;
        let future_rewards_usd = integer_mate::full_math_u128::mul_div_floor(
            (time_until_next_epoch as u128),
            gauge.usd_reward_rate,
            1 << 64
        );

        (future_rewards_usd as u64, time_until_next_epoch)
    }

    /// Internal function to calculate and update reward rates based on new usd rewards.
    /// This handles epoch-based reward distribution logic.
    /// Supposed to be called at the begining of every epoch. 
    /// Does not update the oSAIL distribution rate.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `usd_amount` - The amount of USD to distribute, decimals 6
    /// * `clock` - The system clock
    ///
    /// # Aborts
    /// * If the calculated reward rate is zero
    /// * If there are insufficient reserves for the calculated reward rate
    fun notify_reward_amount_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        usd_amount: u64,
        clock: &sui::clock::Clock
    ) {
        let current_time = clock.timestamp_ms() / 1000;
        let (future_rewards, time_until_next_epoch) = calc_future_rewards(gauge, current_time);
        gauge.usd_reward_rate = integer_mate::full_math_u128::mul_div_floor(
            (usd_amount as u128) + (future_rewards as u128),
            1 << 64,
            time_until_next_epoch as u128
        );
        if (gauge.usd_reward_rate_by_epoch.contains(common::epoch_start(current_time))) {
            gauge.usd_reward_rate_by_epoch.remove(common::epoch_start(current_time));
        };
        gauge.usd_reward_rate_by_epoch.add(common::epoch_start(current_time), gauge.usd_reward_rate);
        assert!(gauge.usd_reward_rate != 0, ENotifyRewardAmountRewardRateZero);
        let notify_reward_event = EventNotifyReward {
            sender: *gauge.voter.borrow(),
            usd_amount,
        };
        sui::event::emit<EventNotifyReward>(notify_reward_event);
    }


    /// Updates the oSAIL distribution rate based on the current price of the oSAIL token
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `price_q64` - USD amount/oSAIL amount * 2^64 assuming both are in same decimals
    /// * `no_emit` - If true, no event will be emitted
    /// * `clock` - The system clock
    fun sync_o_sail_distribution_price_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        price_q64: u128,
        no_emit: bool,
        clock: &sui::clock::Clock,
    ) {
        let current_time = clock.timestamp_ms() / 1000;
        assert!(current_time < gauge.period_finish, ESyncFullsailDistributionPriceInvalidEpoch);
        pool.update_fullsail_distribution_growth_global(gauge.gauge_cap.borrow(), clock);
        let current_distribution_reserve = pool.get_fullsail_distribution_reserve();

        let current_epoch = common::to_period(gauge.epoch_token_last_notified);

        // Update emissions using distribution reserve delta
        if (gauge.last_distribution_reserve > 0) {
            let distributed_amount = gauge.last_distribution_reserve - current_distribution_reserve;
            let current_emission = if (gauge.o_sail_emission_by_epoch.contains(current_epoch)) {
                gauge.o_sail_emission_by_epoch.remove(current_epoch)
            } else {
                0
            };
            gauge.o_sail_emission_by_epoch.add(current_epoch, current_emission + distributed_amount);
        };

        let next_epoch_time = common::epoch_next(current_time);
        let time_until_next_epoch = next_epoch_time - current_time;
        let remaining_usd_amount_q64 = gauge.usd_reward_rate * (time_until_next_epoch as u128);
        let remaining_o_sail_amount_q64 = common::usd_q64_to_asset_q64(remaining_usd_amount_q64, price_q64);
        let o_sail_reward_rate = remaining_o_sail_amount_q64 / (time_until_next_epoch as u128);
        let remaining_o_sail_amount = (remaining_o_sail_amount_q64 >> 64) as u64; // remaining_o_sail_amount_q64 / 2^64

        gauge.o_sail_reward_rate = o_sail_reward_rate;
        gauge.last_distribution_reserve = remaining_o_sail_amount;

        pool.sync_fullsail_distribution_reward(
            gauge.gauge_cap.borrow(),
            o_sail_reward_rate,
            remaining_o_sail_amount,
            next_epoch_time,
            clock
        );

        if (!no_emit) {
            let sync_o_sail_distribution_price_event = EventSyncOSailDistributionPrice {
                gauge_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
                pool_id: object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool),
                price_q64,
                remaining_o_sail_q64: remaining_o_sail_amount_q64,
                remaining_usd_q64: remaining_usd_amount_q64,
            };
            sui::event::emit<EventSyncOSailDistributionPrice>(sync_o_sail_distribution_price_event);
        }
    }

    /// Updates the oSAIL distribution rate based on the current price of the oSAIL token.
    /// The idea is that gauge reward rate represents the USD valuation of the oSAIL token to be distributed.
    /// In this function we calculate the remaining USD valuation to be distributed and convert it into oSAIL amount.
    /// And then we update the pool's oSAIL distribution rate to reflect the new oSAIL amount to be distributed.
    /// This method is supposed to be called once every 15 minutes or so.
    /// More often calls will result in more accurate distribution and less frequent calls will result in less accurate distribution.
    /// The accuracy of the distribution is defined by how close the USD valuation of the oSAIL tokens at the time of distribution is
    /// to the expected USD valuation to be distributed.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `distribution_config` - The distribution config
    /// * `pool` - The associated pool
    /// * `o_sail_price_q64` - The oSAIL price in USD in Q64.64 format, in SAIL token decimals
    /// * `clock` - The system clock
    ///
    public(package) fun sync_o_sail_distribution_price<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        o_sail_price_q64: u128,
        clock: &sui::clock::Clock,
    ) {
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            ESyncOsailDistributionPriceDistributionConfInvalid
        );
        assert!(distribution_config.is_gauge_alive(object::id(gauge)), ESyncOsailDistributionPriceGaugeNotAlive);
        assert!(gauge.check_gauger_pool(pool), ESyncOsailDistributionPriceInvalidPool);

        gauge.sync_o_sail_distribution_price_internal(pool, o_sail_price_q64, false, clock);
    }

    public fun o_sail_emission_by_epoch<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        epoch_start: u64
    ): u64 {
        *gauge.o_sail_emission_by_epoch.borrow(epoch_start)
    }

    /// Returns the timestamp when the current reward period ends.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    ///
    /// # Returns
    /// The timestamp (in seconds) when the current reward period finishes
    public fun period_finish<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>
    ): u64 {
        gauge.period_finish
    }

    /// Receives and stores a gauge capability that allows this gauge to interact with its pool.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `gauge_cap` - Gauge capability to store
    ///
    /// # Aborts
    /// * If the gauge cap does not match the gauge's pool
    public(package) fun receive_gauge_cap<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        gauge_cap: gauge_cap::gauge_cap::GaugeCap
    ) {
        assert!(gauge.pool_id == gauge_cap.get_pool_id(), EReceiveGaugeCapGaugeDoesNotMatch);
        gauge.gauge_cap.fill(gauge_cap);
    }

    /// Returns the current USD reward rate of the gauge
    /// Represents the amount of USD to distribute per second times 2^64
    public fun usd_reward_rate<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>
    ): u128 {
        gauge.usd_reward_rate
    }

    /// Returns the current oSAIL reward rate of the gauge
    /// Represents the amount of oSAIL to distribute per second times 2^64
    /// Is changed by sync_o_sail_distribution_price_internal
    public fun o_sail_reward_rate<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>
    ): u128 {
        gauge.o_sail_reward_rate
    }

    /// Returns the max reward rate that was set during specific epoch.
    /// Reward rate may increase during the epoch because of the new notify_reward calls.
    public fun usd_reward_rate_by_epoch_start<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        epoch_start_time: u64
    ): u128 {
        *gauge.usd_reward_rate_by_epoch.borrow(epoch_start_time)
    }

    /// Sets the voter ID for the gauge. This establishes which voter can direct rewards to this gauge.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `voter_id` - ID of the voter to set
    public(package) fun set_voter<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        voter_id: ID
    ) {
        gauge.voter.fill(voter_id);
        let gauge_set_voter_event = EventGaugeSetVoter {
            id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            voter_id,
        };
        sui::event::emit<EventGaugeSetVoter>(gauge_set_voter_event);
    }

    /// Returns true if RewardCoinType matches current epoch token
    ///
    /// # Arguments
    /// * `<RewardCoinType>` - The coin type to be checked.
    /// * `gauge` - The gauge contract reference
    public fun is_valid_epoch_token<CoinTypeA, CoinTypeB, RewardCoinType>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
    ): bool {
        let coin_type = type_name::get<RewardCoinType>();

        gauge.current_epoch_token.borrow() == &coin_type
    }

    public fun borrow_epoch_token<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
    ): &TypeName {
        gauge.current_epoch_token.borrow()
    }

    /// Destroys a staked position.
    ///
    /// # Arguments
    /// * `staked_positions` - The staked position to destroy
    fun destroy_staked_positions(
        staked_positions: StakedPosition
    ) {
        let StakedPosition{
            id: staked_position_id,
            position_id: _,
            pool_id: _,
            description: _,
            name: _,
        } = staked_positions;        

        sui::object::delete(staked_position_id);
    }

    /// Internal function to handle reward claiming for a specific position.
    /// Updates the reward accounting, calculates the earned amount and returns it.
    /// Does not consider the reward token.
    /// This version includes liquidity update cooldown check.
    public(package) fun update_reward_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ): (u64, u128) {
        assert!(gauge.check_gauger_pool(pool), EUpdateRewardGaugeDoesNotMatchPool);
        let gauge_id = object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge);
        let restricted = gauge.is_withdrawal_restricted(distribution_config, position_id, clock, ctx);
        let current_time = clock.timestamp_ms() / 1000;
        let (amount_earned, growth_inside) = gauge.earned_internal<CoinTypeA, CoinTypeB>(pool, position_id, current_time);
        
        let reward_profile = gauge.rewards.borrow_mut(position_id);

        pool.update_fullsail_distribution_growth_global(gauge.gauge_cap.borrow(), clock);

        reward_profile.amount = reward_profile.amount + amount_earned;

        let amount_to_pay = reward_profile.amount;
        reward_profile.growth_inside = growth_inside;
        let update_reward_event = EventUpdateRewardPosition {
            gauger_id: gauge_id,
            pool_id: object::id(pool),
            position_id,
            growth_inside: reward_profile.growth_inside,
            amount: reward_profile.amount,
        };
        sui::event::emit<EventUpdateRewardPosition>(update_reward_event);

        // Check liquidity update cooldown
        if (restricted) {
            return (0, growth_inside)
        };

        reward_profile.amount = 0;

        (amount_to_pay, growth_inside)
    }

    public fun all_rewards_claimed<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position_id: ID,
        clock: &sui::clock::Clock,
    ): bool {
        assert!(gauge.check_gauger_pool(pool), EAllRewardsClaimedGaugeDoesNotMatchPool);

        let (earned, _) = gauge.earned_internal<CoinTypeA, CoinTypeB>(
            pool,
            position_id,
            clock.timestamp_ms() / 1000
        );

        earned == 0
    }

    /// Withdraws a staked position from the gauge and returns it to its owner.
    /// Only claims rewards in last coin. Fails if there are unclaimed rewards from previous epochs.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `staked_position` - The staked position to withdraw
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Aborts
    /// * If the position is not deposited in the gauge
    /// * If the position hasn't been properly received by the gauge
    /// * If the sender is not the owner of the position
    public fun withdraw_position<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: StakedPosition,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): clmm_pool::position::Position {
        distribution_config.checked_package_version();
        assert!(gauge.check_gauger_pool(pool), EUpdateRewardGaugeDoesNotMatchPool);
        assert!(
            gauge.staked_positions.contains(staked_position.position_id),
            EWithdrawPositionNotDepositedPosition
        );
        assert!(!gauge.locked_positions.contains(staked_position.position_id), EWithdrawPositionPositionIsLocked);

        let position = gauge.withdraw_position_internal_old<CoinTypeA, CoinTypeB>(
            pool,
            staked_position,
            clock,
        );

        position
    }

    public fun withdraw_position_v2<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        global_config: &clmm_pool::config::GlobalConfig,
        vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: StakedPosition,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ): clmm_pool::position::Position {
        distribution_config.checked_package_version();
        assert!(gauge.check_gauger_pool(pool), EUpdateRewardGaugeDoesNotMatchPool);
        assert!(
            gauge.staked_positions.contains(staked_position.position_id),
            EWithdrawPositionNotDepositedPosition
        );
        assert!(!gauge.locked_positions.contains(staked_position.position_id), EWithdrawPositionPositionIsLocked);

        let position = gauge.withdraw_position_internal<CoinTypeA, CoinTypeB>(
            pool,
            distribution_config,
            global_config,
            vault,
            staked_position,
            clock,
            ctx
        );

        position
    }

    /// Withdraws a position from the gauge through the locker.
    /// This function allows the locker to withdraw a position from the gauge, collecting any earned rewards
    /// and unstaking the position from the pool's distribution system.
    /// 
    /// # Arguments
    /// * `gauge` - Mutable reference to the gauge contract managing rewards
    /// * `_locker_cap` - Capability proving locker authorization
    /// * `pool` - Mutable reference to the CLMM pool where the position exists
    /// * `staked_position` - The staked position to withdraw
    /// * `clock` - System clock for time-based operations
    /// * `ctx` - Transaction context
    /// 
    /// # Returns
    /// The withdrawn position
    /// 
    /// # Aborts
    /// * If the position is not staked in the gauge
    /// * If the reward token type is not valid for the current epoch
    /// * If the position has not been properly received
    public fun withdraw_position_by_locker<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        _locker_cap: &locker_cap::locker_cap::LockerCap,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: StakedPosition,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext,
    ): clmm_pool::position::Position {
        abort
    }

    /// Internal function to withdraw a staked position from the gauge.
    /// User is supposed to claim all rewards before withdrawing.
    /// The claim could be placed in the PTB right before the withdraw.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance
    /// * `pool` - The associated pool
    /// * `staked_position` - The staked position to withdraw
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// The withdrawn position
    ///
    /// # Aborts
    /// * If the position is not staked in the gauge
    /// * If the reward token type is not valid for the current epoch
    /// * If the position has not been properly received by the gauge
   fun withdraw_position_internal<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        global_config: &clmm_pool::config::GlobalConfig,
        vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        staked_position: StakedPosition,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ): clmm_pool::position::Position {

        assert!(gauge.check_gauger_pool(pool), EWithdrawPositionInvalidPool);
        assert!(gauge.all_rewards_claimed<CoinTypeA, CoinTypeB>(pool, staked_position.position_id, clock), EWithdrawPositionNotAllRewardsClaimed);

        let reward_profile = gauge.rewards.borrow_mut(staked_position.position_id);
        if (reward_profile.amount > 0) {
            gauge.add_unclaimed_o_sail(reward_profile.amount);
        };

        let staked_position_id = object::id<StakedPosition>(&staked_position);

        let mut position = gauge.staked_positions.remove(staked_position.position_id);
        pool.unstake_from_fullsail_distribution(
            gauge.gauge_cap.borrow(),
            &position,
            clock
        );

        destroy_staked_positions(staked_position);

        gauge.collect_early_withdrawal_penalty(
            distribution_config,
            global_config,
            vault,
            pool,
            &mut position,
            clock,
            ctx
        );

        let withdraw_position_event = EventWithdrawPosition {
            staked_position_id,
            position_id: object::id<clmm_pool::position::Position>(&position),
            gauger_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            pool_id:  object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool),
        };
        sui::event::emit<EventWithdrawPosition>(withdraw_position_event);

        position
    }

    fun withdraw_position_internal_old<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: StakedPosition,
        clock: &sui::clock::Clock,
    ): clmm_pool::position::Position {

        assert!(gauge.check_gauger_pool(pool), EWithdrawPositionInvalidPool);
        assert!(gauge.all_rewards_claimed<CoinTypeA, CoinTypeB>(pool, staked_position.position_id, clock), EWithdrawPositionNotAllRewardsClaimed);

        let reward_profile = gauge.rewards.borrow_mut(staked_position.position_id);
        if (reward_profile.amount > 0) {
            gauge.add_unclaimed_o_sail(reward_profile.amount);
        };

        let staked_position_id = object::id<StakedPosition>(&staked_position);

        let position = gauge.staked_positions.remove(staked_position.position_id);
        pool.unstake_from_fullsail_distribution(
            gauge.gauge_cap.borrow(),
            &position,
            clock
        );

        destroy_staked_positions(staked_position);

        let withdraw_position_event = EventWithdrawPosition {
            staked_position_id,
            position_id: object::id<clmm_pool::position::Position>(&position),
            gauger_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            pool_id:  object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool),
        };
        sui::event::emit<EventWithdrawPosition>(withdraw_position_event);

        position
    }

    fun collect_early_withdrawal_penalty<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        global_config: &clmm_pool::config::GlobalConfig,
        vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        position: &mut clmm_pool::position::Position,
        clock: &sui::clock::Clock,
        ctx: &TxContext
    ) {

        if (!gauge.is_withdrawal_restricted(distribution_config, object::id<clmm_pool::position::Position>(position), clock, ctx)) {
            return
        };

        let liquidity = position.liquidity();
        if (liquidity == 0) {
            return
        };

        let early_withdrawal_penalty_percentage = distribution_config.get_early_withdrawal_penalty_percentage();
        if (early_withdrawal_penalty_percentage == 0) {
            return
        };

        let early_withdrawal_penalty = integer_mate::full_math_u128::mul_div_floor(
            liquidity,
            early_withdrawal_penalty_percentage as u128,
            governance::distribution_config::get_early_withdrawal_penalty_multiplier() as u128
        );
        if (early_withdrawal_penalty == 0) {
            return
        };

        let (penalty_a, penalty_b) = clmm_pool::pool::remove_liquidity<CoinTypeA, CoinTypeB>(
            global_config,
            vault,
            pool, 
            position, 
            early_withdrawal_penalty, 
            clock
        );

        let penalty_amount_a = penalty_a.value();
        let penalty_amount_b = penalty_b.value();

        let amount_a = gauge.fee_a.join<CoinTypeA>(penalty_a);
        let amount_b = gauge.fee_b.join<CoinTypeB>(penalty_b);

        let event = EventCollectEarlyWithdrawalPenalty {
            gauge_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            position_id: object::id<clmm_pool::position::Position>(position),
            early_withdrawal_penalty: early_withdrawal_penalty,
            penalty_amount_a: penalty_amount_a,
            penalty_amount_b: penalty_amount_b,
            fee_a: amount_a,
            fee_b: amount_b,
            timestamp: clock.timestamp_ms() / 1000,
        };
        sui::event::emit<EventCollectEarlyWithdrawalPenalty>(event);
    }
    
    /// Mark a position as locked in the gauge
    /// 
    /// # Arguments
    /// * `_locker_cap` - The locker capability
    /// * `gauge` - The gauge containing the position
    /// * `position_id` - ID of the position to lock
    public fun lock_position<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        _locker_cap: &locker_cap::locker_cap::LockerCap,
        position_id: ID,
    ) {
        // this method is called by the locker package, so version control is solved by the locker package.
        if (!gauge.locked_positions.contains(position_id)) {
            gauge.locked_positions.add(position_id, Locked {});
        }
    }

    /// Remove the locked status from a position in the gauge
    /// 
    /// # Arguments
    /// * `_locker_cap` - The locker capability
    /// * `gauge` - The gauge containing the position
    /// * `position_id` - ID of the position to unlock
    public fun unlock_position<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        _locker_cap: &locker_cap::locker_cap::LockerCap,
        position_id: ID,
    ) {
        // this method is called by the locker package, so version control is solved by the locker package.
        if (gauge.locked_positions.contains(position_id)) {
            gauge.locked_positions.remove(position_id);
        }
    }

    /// Decreases liquidity from a staked position while keeping it staked in the gauge.
    /// This function allows partial withdrawal of liquidity from a position without fully withdrawing it.
    /// The position remains staked and continues to earn rewards on the remaining liquidity.
    /// For complete withdrawal of the position, use `withdraw_position` instead.
    ///
    /// # Arguments
    /// * `gauge` - The gauge instance managing the staked position
    /// * `distribution_config` - The distribution configuration
    /// * `global_config` - The CLMM pool global configuration
    /// * `vault` - The rewarder global vault
    /// * `pool` - The associated CLMM pool
    /// * `staked_position` - The staked position to decrease liquidity from
    /// * `liquidity` - The amount of liquidity to remove (must be less than current position liquidity)
    /// * `clock` - The system clock
    /// * `ctx` - Transaction context
    ///
    /// # Returns
    /// A tuple containing the balances of CoinTypeA and CoinTypeB removed from the position
    ///
    /// # Aborts
    /// * If the distribution config does not match the gauge's distribution config
    /// * If the pool does not match the gauge's associated pool
    /// * If the position is not staked in the gauge
    /// * If the position is locked
    /// * If there are unclaimed rewards from previous epochs
    /// * If the requested liquidity amount is greater than or equal to the position's current liquidity
    public fun decrease_liquidity<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        distribution_config: &DistributionConfig,
        global_config: &clmm_pool::config::GlobalConfig,
        vault: &mut clmm_pool::rewarder::RewarderGlobalVault,
        pool: &mut clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>,
        staked_position: &StakedPosition,
        liquidity: u128,
        clock: &sui::clock::Clock,
        ctx: &mut TxContext
    ): (sui::balance::Balance<CoinTypeA>, sui::balance::Balance<CoinTypeB>) {
        assert!(
            object::id(distribution_config) == gauge.distribution_config,
            EDepositPositionDistributionConfInvalid
        );
        distribution_config.checked_package_version();
        assert!(
            gauge.check_gauger_pool(pool),
            EDepositPositionGaugeDoesNotMatchPool
        );
        assert!(
            gauge.staked_positions.contains(staked_position.position_id),
            EWithdrawPositionNotDepositedPosition
        );
        assert!(!gauge.locked_positions.contains(staked_position.position_id), EWithdrawPositionPositionIsLocked);
        assert!(gauge.all_rewards_claimed<CoinTypeA, CoinTypeB>(pool, staked_position.position_id, clock), EWithdrawPositionNotAllRewardsClaimed);

        let position_id = staked_position.position_id();
        let pool_id = object::id<clmm_pool::pool::Pool<CoinTypeA, CoinTypeB>>(pool);

        let mut position = gauge.staked_positions.remove(staked_position.position_id);
        assert!(pool_id == position.pool_id(), EDepositPositionGaugeDoesNotMatchPool);

        pool.unstake_from_fullsail_distribution(
            gauge.gauge_cap.borrow(),
            &position,
            clock
        );

        assert!(liquidity < clmm_pool::position::liquidity(&position), EInvalidLiquidity);

        let (removed_a, removed_b) = clmm_pool::pool::remove_liquidity<CoinTypeA, CoinTypeB>(
            global_config,
            vault,
            pool, 
            &mut position,
            liquidity, 
            clock
        );

        let (lower_tick, upper_tick) = position.tick_range();
        let gauge_cap = gauge.gauge_cap.borrow();
        pool.update_fullsail_distribution_growth_global(gauge_cap, clock);
        if (!gauge.rewards.contains(position_id)) {
            let new_reward_profile = RewardProfile {
                growth_inside: pool.get_fullsail_distribution_growth_inside(lower_tick, upper_tick, 0),
                amount: 0,
                last_update_time: clock.timestamp_ms() / 1000,
            };
            gauge.rewards.add(position_id, new_reward_profile);
        } else {
            let reward_profile = gauge.rewards.borrow_mut(position_id);
            reward_profile.growth_inside = pool.get_fullsail_distribution_growth_inside(lower_tick, upper_tick, 0);
            reward_profile.last_update_time = clock.timestamp_ms() / 1000;
        };

        pool.stake_in_fullsail_distribution(
            gauge_cap,
            &position,
            clock
        );

        gauge.staked_positions.add(position_id, position);

        (removed_a, removed_b)
    }

    fun update_display(
        publisher: &sui::package::Publisher,
        name: std::string::String,
        link: std::string::String,
        image_url: std::string::String,
        description: std::string::String,
        project_url: std::string::String,
        creator: std::string::String,
        ctx: &mut sui::tx_context::TxContext
    ): sui::display::Display<StakedPosition> {
        let mut keys = std::vector::empty<std::string::String>();
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"name"));
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"link"));
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"image_url"));
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"description"));
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"project_url"));
        std::vector::push_back<std::string::String>(&mut keys, std::string::utf8(b"creator"));

        let mut values = std::vector::empty<std::string::String>();
        std::vector::push_back<std::string::String>(&mut values, name);
        std::vector::push_back<std::string::String>(&mut values, link);
        std::vector::push_back<std::string::String>(&mut values, image_url);
        std::vector::push_back<std::string::String>(&mut values, description);
        std::vector::push_back<std::string::String>(&mut values, project_url);
        std::vector::push_back<std::string::String>(&mut values, creator);

        let mut display = sui::display::new_with_fields<StakedPosition>(publisher, keys, values, ctx);

        sui::display::update_version<StakedPosition>(&mut display);

        display
    }

    /// Sets up display fields for a staked position with custom metadata.
    /// This function configures how the staked position will be displayed in the Fullsail Finance interface.
    /// 
    /// # Arguments
    /// * `global_config` - Reference to the global configuration
    /// * `publisher` - Reference to the package publisher
    /// * `description` - Custom description for the staked position
    /// * `link` - Custom link to the staked position in Fullsail Finance app
    /// * `project_url` - URL of the project website
    /// * `creator` - Name of the staked position creator
    /// * `ctx` - Mutable reference to the transaction context
    /// 
    /// # Details
    /// Sets up the following display fields:
    /// * name - Staked position name (template: "{name}")
    /// * link - Custom staked position link
    /// * image_url - Staked position image URL (template: "{image_url}")
    /// * description - Custom description
    /// * project_url - Custom project website
    /// * creator - Custom creator name
    /// 
    /// # Abort Conditions
    /// * If the package version check fails
    public fun set_display(
        publisher: &sui::package::Publisher,
        name: std::string::String,
        link: std::string::String,
        image_url: std::string::String,
        description: std::string::String,
        project_url: std::string::String,
        creator: std::string::String,
        ctx: &mut sui::tx_context::TxContext
    ) {
        assert!(publisher.from_module<GAUGE>(), ENotOwner);

        let display = update_display(
            publisher,
            name,
            link,
            image_url,
            description,
            project_url,
            creator,
            ctx
        );

        sui::transfer::public_transfer<sui::display::Display<StakedPosition>>(display, sui::tx_context::sender(ctx));
    }

    public fun name(staked_position: &StakedPosition): std::string::String {
        staked_position.name
    }

    public fun description(staked_position: &StakedPosition): std::string::String {
        staked_position.description
    }

    public fun get_pool_id(staked_position: &StakedPosition): sui::object::ID {
        staked_position.pool_id
    }

    public fun position_index<CoinTypeA, CoinTypeB>(gauge: &Gauge<CoinTypeA, CoinTypeB>, staked_position: &StakedPosition): u64 {
        gauge.staked_positions.borrow(staked_position.position_id).index()
    }

    public fun borrow_position<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>, 
        staked_position: &StakedPosition
    ): &clmm_pool::position::Position {
        abort
    }

    public fun position_liquidity<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>, 
        staked_position: &StakedPosition
    ): u128 {
        gauge.staked_positions.borrow(staked_position.position_id).liquidity()
    }

    public fun position_tick_range<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>, 
        staked_position: &StakedPosition
    ): (integer_mate::i32::I32, integer_mate::i32::I32) {
        gauge.staked_positions.borrow(staked_position.position_id).tick_range()
    }

    /// Adds unclaimed o_sail amount to the gauge
    /// If the value already exists in the bag, it will be added to the existing amount.
    /// If it doesn't exist, a new entry will be created with the provided amount.
    /// 
    /// # Arguments
    /// * `gauge` - Mutable reference to the gauge
    /// * `amount` - The amount of unclaimed o_sail to add
    public(package) fun add_unclaimed_o_sail<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        amount: u64,
    ) {
        let current_amount = if (gauge.bag.contains(UNCLAIMED_O_SAIL_KEY)) {
            gauge.bag.remove<vector<u8>, u64>(UNCLAIMED_O_SAIL_KEY)
        } else {
            0
        };
        
        gauge.bag.add(UNCLAIMED_O_SAIL_KEY, current_amount + amount);

        let event = EventAddUnclaimedOsail {
            gauge_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            amount: amount,
        };
        sui::event::emit<EventAddUnclaimedOsail>(event);
    }

    /// Removes and returns the unclaimed o_sail amount from the gauge
    /// This method removes the value from the bag and returns it.
    /// 
    /// # Arguments
    /// * `gauge` - Mutable reference to the gauge
    /// 
    /// # Returns
    /// The unclaimed o_sail amount (returns 0 if not set)
    public(package) fun remove_unclaimed_o_sail<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
    ): u64 {
        let amount = if (gauge.bag.contains(UNCLAIMED_O_SAIL_KEY)) {
            gauge.bag.remove<vector<u8>, u64>(UNCLAIMED_O_SAIL_KEY)
        } else {
            0
        };

        let event = EventRemoveUnclaimedOsail {
            gauge_id: object::id<Gauge<CoinTypeA, CoinTypeB>>(gauge),
            amount: amount,
        };
        sui::event::emit<EventRemoveUnclaimedOsail>(event);

        amount
    }

    public fun get_unclaimed_o_sail<CoinTypeA, CoinTypeB>(
        gauge: &Gauge<CoinTypeA, CoinTypeB>,
    ): u64 {
        if (gauge.bag.contains(UNCLAIMED_O_SAIL_KEY)) {
            *gauge.bag.borrow<vector<u8>, u64>(UNCLAIMED_O_SAIL_KEY)
        } else {
            0
        }
    }

    #[test_only]
    public fun test_update_reward_profile<CoinTypeA, CoinTypeB>(
        gauge: &mut Gauge<CoinTypeA, CoinTypeB>,
        staked_position: &StakedPosition,
        last_update_time: u64
    ){
        let reward_profile = gauge.rewards.borrow_mut(staked_position.position_id);
        reward_profile.last_update_time = last_update_time;
    }
}
